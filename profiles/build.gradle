apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: '../gradle/common.gradle'

shadowJar {
    mergeServiceFiles {
        include "META-INF/services/io.vertx.core.spi.VerticleFactory"
    }
    manifest {
        attributes 'Main-Class': 'profiles.Launcher'
        attributes 'Main-Verticle': 'profiles.verticles.MainVerticle'
    }
}

dependencies {
    implementation project(':common')

    implementation group: 'io.vertx', name: 'vertx-grpc', version: "${vertxVersion}"
    implementation group: 'io.vertx', name: 'vertx-config', version: "${vertxVersion}"

    implementation group: 'io.grpc', name: 'grpc-services', version: '1.23.0'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.9.1'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

sourceSets {
    main {
        java {
            srcDir 'build/generated/source/proto/main/grpc'
            srcDir 'build/generated/source/proto/main/java'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.9.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.23.0'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}